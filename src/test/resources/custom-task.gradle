plugins {
    id 'pl.droidsonroids.jacoco.testkit'
    id 'java'
    id 'jacoco'
}

repositories {
    mavenCentral()
}

// Adding integration tests as shown at: https://docs.gradle.org/current/userguide/java_testing.html#sec:configuring_java_integration_tests
sourceSets {
    intTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

task integrationTest(type: Test) {
    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}

// Configure jacoco testkit plugin
jacocoTestKit {
    applyTo("intTestRuntimeOnly", tasks.named("integrationTest"))
}
