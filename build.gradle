import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.8.21'
    id 'com.gradle.plugin-publish' version '1.2.0'
    id 'jacoco'
    id 'java-gradle-plugin'
    id 'pl.droidsonroids.jacoco.testkit' version '1.0.12'
}

dependencies {
    compileOnly gradleApi()
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.assertj:assertj-core:3.24.2'
}

group = GROUP
version = VERSION_NAME

gradlePlugin {
    website = POM_URL
    vcsUrl = POM_SCM_URL

    plugins {
        jacocoTestKitPlugin {
            id = 'pl.droidsonroids.jacoco.testkit'
            implementationClass = 'pl.droidsonroids.gradle.jacoco.testkit.JaCoCoTestKitPlugin'
            displayName = POM_NAME
            description = POM_DESCRIPTION
            tags.set(['jacoco', 'coverage', 'testkit', 'test'])
        }
    }
}

kotlin {
    jvmToolchain(17)
}

def javaTargetVersion = JavaVersion.VERSION_1_8
tasks.withType(KotlinCompile).configureEach {
    kotlinOptions.jvmTarget = javaTargetVersion.toString()
}
tasks.withType(JavaCompile).configureEach {
    options.release.set(javaTargetVersion.majorVersion.toInteger())
}

tasks.withType(Test).configureEach {
    doLast {
        Thread.sleep(2000) // https://github.com/gradle/gradle/issues/16603
    }
}

jacoco {
    toolVersion '0.8.10'
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
        html.required = true
    }
}
